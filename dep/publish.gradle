apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    repositories {
        maven {
            url 'http://artifactory.58corp.com:8081/artifactory/android-local'
            credentials {
                username MAVEN_USERNAME
                password MAVEN_PASSWORD
            }
        }
    }

    publications {
        aar(MavenPublication) {
            groupId 'com.wuba.wuxian.sdk'
            artifactId 'dep'
            version "0.0.1"

            artifact file("$buildDir/outputs/aar/dep-release.aar")
            artifact sourcesJar

            pom.withXml(createPomDependencies(["implementation"]))
        }
    }
}

project.afterEvaluate {
    publish.dependsOn assembleRelease
    publish.mustRunAfter assembleRelease
}

// The publications doesn't know about our AAR dependencies, so we have to manually add them to the pom
// Credit: http://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
def createPomDependencies(configurationNames) {
    return {
        def dependenciesNode = asNode().appendNode('dependencies')
        configurationNames.each { configurationName ->
            configurations[configurationName].allDependencies.each {
                if (it.group != null && it.name != null) {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)

                    //If there are any exclusions in dependency
                    if (it.excludeRules.size() > 0) {
                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        it.excludeRules.each { rule ->
                            def exclusionNode = exclusionsNode.appendNode('exclusion')
                            exclusionNode.appendNode('groupId', rule.group)
                            exclusionNode.appendNode('artifactId', rule.module)
                        }
                    }
                }
            }
        }
    }
}
